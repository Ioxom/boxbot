plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

mainClassName = 'ca.ioxom.boxbot.Main'
group 'ca.ioxom'
version '0.1.0'
def jdaVersion = '4.2.0_227'
def jacksonVersion = '2.11.0'

repositories {
    jcenter()
}

dependencies {
    //json parsing library
    implementation(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion)
    //integration with discord
    implementation(group: 'net.dv8tion', name: 'JDA', version: jdaVersion)
}

//we use java 11
compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.encoding = 'UTF-8'
}

//append the version to a properties file so we can access it from java code
task('addVersion') {
    doLast {
        file("src/main/resources/boxbot.properties").write(
                "# boxbot's version. this is stored here to allow project-wide version syncing\n" +
                        "version=$version"
        )
    }
}

//create a jar that includes dependencies so it can be run
task uberJar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName, 'Manifest-Version': 1.0
    }

    from sourceSets.main.output

    DuplicatesStrategy.EXCLUDE
    dependsOn(addVersion)
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}